# Maintainer: Keith Hughitt <khughitt@umd.edu>
# Contributor: Thiago Yukio Kikuchi Oliveira <stratust at gmail dot com>
pkgname=tophat
pkgver=2.0.12
pkgrel=1
pkgdesc="TopHat is a fast splice junction mapper for RNA-Seq reads."
arch=('x86_64' 'i686')
url="http://tophat.cbcb.umd.edu"
license=('custom')
depends=('boost' 'python2' 'samtools' 'bowtie2' 'perl')
source=(http://ccb.jhu.edu/software/tophat/downloads/$pkgname-$pkgver.tar.gz)
md5sums=('b419604b45d94f6e878a141f05dd1d75')

build() {
  # Tophat use python2 but archlinux uses python3 as default python
  cd $srcdir/$pkgname-$pkgver/src
  python_files="bed_to_juncs contig_to_chr_coords sra_to_solid tophat.py tophat-fusion-post"
  sed -i "s|#!/usr/bin/env python$|#!/usr/bin/env python2|" $python_files

  cd $srcdir/$pkgname-$pkgver

  export LDFLAGS="-lboost_system"
  export MAKEFLAGS="-j1"

  ./configure --prefix="/usr"

  make DESTDIR="$pkgdir"
}

package() {
  cd $srcdir/$pkgname-$pkgver/src
  make install DESTDIR="$pkgdir"

  cd $srcdir/$pkgname-$pkgver
  install -D -m644 LICENSE "${pkgdir}/usr/share/licenses/${pkgname}/LICENSE"
}
